# Run Omnara as a background daemon service.
{
  config,
  lib,
  omnara,
  pkgs,
  ...
}:
let
  isLinux = pkgs.hostPlatform.isLinux;
  isDarwin = pkgs.hostPlatform.isDarwin;

  homeDir = config.home.homeDirectory;

  # Run as a login shell so we get normal PATH + aliases
  execStart = [
    "${pkgs.bashInteractive}/bin/bash"
    "-lc"
    "'exec ${omnara}/bin/omnara daemon run-service'"
  ];
in
{
  # Add Omnara to PATH
  home.packages = [ omnara ];

  # Linux - systemd user service
  systemd.user.services.omnara = lib.mkIf isLinux {
    Install.WantedBy = [ "default.target" ];

    Unit = {
      Description = "Omnara Background Daemon";
      After = [ "network.target" ];
    };

    Service = {
      Type = "simple";
      ExecStart = builtins.concatStringsSep " " execStart;
      # Nix manages the version; prevent self-update restart loops
      Environment = "OMNARA_NO_UPDATE=1";
      Restart = "on-failure";
      RestartSec = 5;
      WorkingDirectory = homeDir;
    };
  };

  # macOS - launchd agent
  launchd.agents.omnara = lib.mkIf isDarwin {
    enable = true;
    config = {
      ProcessType = "Background";
      ProgramArguments = execStart;
      WorkingDirectory = homeDir;
      # Nix manages the version; prevent self-update restart loops
      EnvironmentVariables.OMNARA_NO_UPDATE = "1";
      RunAtLoad = true;
      KeepAlive = {
        SuccessfulExit = false;
        Crashed = true;
      };
      ThrottleInterval = 5;
    };
  };
}
